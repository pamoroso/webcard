(DEFINE-FILE-INFO PACKAGE "INTERLISP" READTABLE "INTERLISP" BASE 10)

(FILECREATED "13-Oct-2024 10:55:25" {DSK}<home>paolo>il>webcard>WEBCARD.;15 8389   

      :EDIT-BY "PA"

      :CHANGES-TO (FNS WCD.TraverseWebLink)

      :PREVIOUS-DATE "12-Oct-2024 19:33:33" {DSK}<home>paolo>il>webcard>WEBCARD.;14)


(PRETTYCOMPRINT WEBCARDCOMS)

(RPAQQ WEBCARDCOMS ((* A NoteCards extension for visiting websites.)
                    (FNS WCD.AskURL WCD.CreateWebCardType WCD.EditURLMenuCmd WCD.InitWebCard 
                         WCD.MakeWebCard WCD.TraverseWebLink WCD.UpdateCardText WCD.ValidURLP 
                         WCD.VisitURL WCD.VisitURLMenuCmd)
                    (P (WCD.InitWebCard))
                    (PROP LinkIconLeftButtonFn Web)))



(* A NoteCards extension for visiting websites.)

(DEFINEQ

(WCD.AskURL
  [LAMBDA (MainWindow Default)                               (* ; "Edited  9-Oct-2024 16:50 by PA")
                                                             (* ; "Edited  3-Oct-2024 14:00 by PA")

         (* Ask for a URL in a dialog attached to MainWindow and return the URL.
         Provides Default a s URL prefix that can be edited.
         If MainWindow is NIL then the top level prompt window is used.)
                                                             (* TODO Do at least some URL 
                                                             validation.)
    (bind (URL _ NIL) until URL do (SETQ URL (NCP.AskUser "Enter full URL" ": "
                                                    (if Default
                                                        then Default
                                                      else "")
                                                    T MainWindow NIL NIL T))
       finally (RETURN URL])

(WCD.CreateWebCardType
  [LAMBDA NIL                                                (* ; "Edited  1-Oct-2024 17:35 by PA")
                                                             (* Define the new card type Web that 
                                                             inherits from Text.)
    (NCP.CreateCardType 'Web 'Text '((MakeFn WCD.MakeWebCard))
           `((LinkAnchorModesSupported NIL)
             (DisplayedInMenuFlg T])

(WCD.EditURLMenuCmd
  [LAMBDA (Window)                                           (* ; "Edited 12-Oct-2024 19:32 by PA")

         (* Edit the URL associated with the card when the user selects the relevant 
         option of the card's left-click menu.)

    (LET* [(Card (NCP.CoerceToCard Window))
           (URL (WCD.AskURL Window (NCP.CardProp Card 'URL]
          (NCP.CardProp Card 'URL URL)
          (WCD.UpdateCardText Card URL])

(WCD.InitWebCard
  [LAMBDA NIL                                                (* ; "Edited 11-Oct-2024 18:22 by PA")
                                                             (* Initialize the Web card type.)
    (WCD.CreateWebCardType)
    (NCP.AddTitleBarMenuItemsToType 'Web 'Left '((Visit% URL (FUNCTION WCD.VisitURLMenuCmd)
                                                        "Open the website associated with the card.")
                                                 (Edit% URL (FUNCTION WCD.EditURLMenuCmd)
                                                        "Edit the URL associated with the card."))
           'Top])

(WCD.MakeWebCard
  [LAMBDA (Card Title NoDisplayFlg)                          (* ; "Edited 12-Oct-2024 19:22 by PA")
                                                             (* ; "Edited  9-Oct-2024 14:08 by PA")
                                                             (* ; "Edited  3-Oct-2024 13:58 by PA")
                                                             (* ; "Edited  1-Oct-2024 17:51 by PA")

         (* Make a new card of type Web. Return the window of the new card if NoDisplayFlg 
         is non-NIL, and the ID if NoDisplayFlg is NIL.)

    (if (NOT NoDisplayFlg)
        then (LET* ((Window (NCP.ApplySuperTypeFn MakeFn Card Card Title NoDisplayFlg))
                    (URL (WCD.AskURL Window)))
                   (NCP.CardProp Card 'URL URL)
                   (NCP.CardTitle Card "Untitled")
                   (WCD.UpdateCardText Card URL)
                   Window)
      else (NCP.CardProp Card 'URL (WCD.AskURL))             (* TODO More Text card initialization 
                                                             here.)
           Card])

(WCD.TraverseWebLink
  [LAMBDA (Card Window)                                      (* ; "Edited 13-Oct-2024 10:55 by PA")
                                                             (* ; "Edited  9-Oct-2024 17:16 by PA")
                                                             (* ; "Edited  5-Oct-2024 19:50 by PA")
                                                             (* ; "Edited  4-Oct-2024 18:43 by PA")

         (* Open in the system web browser the URL associated with the destination Web 
         Card. Also opens the card and returns it.
         The Window containing the link icon is ignored.)

    (if (NCP.ValidCardP Card)
        then (NCP.OpenCard Card) 

         (* Make card text read only. Notefiles don't preserve the read only state which 
         is a runtime property, so this is necessary for not yet opened cards from newly 
         loaded notefiles. To catch this the first link traversal triggers read only.)

             (TEXTPROP (NCP.CardSubstance Card)
                    'READONLY T)
             (WCD.VisitURL Card])

(WCD.UpdateCardText
  [LAMBDA (Card NewText)                                     (* ; "Edited 12-Oct-2024 19:26 by PA")
                                                             (* Replace the Card text with NewText.)
    (LET ((TEXTSTREAM (NCP.CardSubstance Card)))

         (* Set the card text to read-write, clear the existing text, insert the new text, 
         and make the text read only again,)

         (TEXTPROP TEXTSTREAM 'READONLY NIL)                 (* Clear card text.)
         (TEDIT.DELETE TEXTSTREAM 1 (TEDIT.NCHARS TEXTSTREAM))
         (NCP.CardAddText Card URL 'END)                     (* Make card text read only.)
         (TEXTPROP TEXTSTREAM 'READONLY T])

(WCD.ValidURLP
  [LAMBDA (String)                                           (* ; "Edited  7-Oct-2024 13:21 by PA")

         (* Return String if it is a valid URL, NIL otherwise.
         A URL is valid if it is not null, contains no spaces or tabs, and starts with 
         https%://, http%://, or mailto%:.)

    (LET ((LowerString (L-CASE String)))
         (if [AND LowerString (STRINGP LowerString)
                  (NOT (STRPOS " " LowerString))
                  (LET ((Length (NCHARS LowerString)))
                       (OR (AND (EQP (STRPOS "https://" LowerString)
                                     1)
                                (GREATERP Length 8))
                           (AND (EQP (STRPOS "http://" LowerString)
                                     1)
                                (GREATERP Length 7))
                           (AND (EQP (STRPOS "mailto:" LowerString)
                                     1)
                                (GREATERP Length 7]
             then String
           else NIL])

(WCD.VisitURL
  [LAMBDA (Card)                                             (* ; "Edited  9-Oct-2024 17:15 by PA")

         (* Visit the URL associated with Card. Commands the browser to of the host 
         operating system to open the URL.)

    (LET [(URL (NCP.CardProp Card 'URL]
         (if URL
             then (ShellBrowse URL])

(WCD.VisitURLMenuCmd
  [LAMBDA (Window)                                           (* ; "Edited 11-Oct-2024 18:03 by PA")

         (* Visit the URL associated with the card when the user selects the relevant 
         option of the card's left-click menu.)

    (WCD.VisitURL (NCP.CoerceToCard Window])
)

(WCD.InitWebCard)

(PUTPROPS Web LinkIconLeftButtonFn WCD.TraverseWebLink)
(DECLARE%: DONTCOPY
  (FILEMAP (NIL (821 8278 (WCD.AskURL 831 . 1873) (WCD.CreateWebCardType 1875 . 2342) (
WCD.EditURLMenuCmd 2344 . 2811) (WCD.InitWebCard 2813 . 3473) (WCD.MakeWebCard 3475 . 4641) (
WCD.TraverseWebLink 4643 . 5782) (WCD.UpdateCardText 5784 . 6504) (WCD.ValidURLP 6506 . 7580) (
WCD.VisitURL 7582 . 7948) (WCD.VisitURLMenuCmd 7950 . 8276)))))
STOP
