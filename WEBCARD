(DEFINE-FILE-INFO PACKAGE "INTERLISP" READTABLE "INTERLISP" BASE 10)

(FILECREATED " 7-Oct-2024 13:24:07" {DSK}<home>paolo>il>webcard>WEBCARD.;7 4605   

      :EDIT-BY "PA"

      :CHANGES-TO (VARS WEBCARDCOMS)
                  (FNS WCD.ValidURLP)

      :PREVIOUS-DATE " 5-Oct-2024 19:51:26" {DSK}<home>paolo>il>webcard>WEBCARD.;6)


(PRETTYCOMPRINT WEBCARDCOMS)

(RPAQQ WEBCARDCOMS ((* A NoteCards extension for visiting websites.)
                    (FNS WCD.AskURL WCD.CreateWebCardType WCD.MakeWebCard WCD.TraverseWebLink 
                         WCD.ValidURLP)
                    (P (WCD.CreateWebCardType))
                    (PROP LinkIconLeftButtonFn Web)))



(* A NoteCards extension for visiting websites.)

(DEFINEQ

(WCD.AskURL
  [LAMBDA (MainWindow)                                       (* ; "Edited  3-Oct-2024 14:00 by PA")

         (* Ask for a URL in a dialog attached to MainWindow and return the URL.
         If MainWindow is NIL then the top level prompt window is used.)
                                                             (* TODO Do at least some URL 
                                                             validation.)
    (bind (URL _ NIL) until URL do (SETQ URL (NCP.AskUser "Enter a URL" "--> " NIL T MainWindow NIL 
                                                    NIL T)) finally (RETURN URL])

(WCD.CreateWebCardType
  [LAMBDA NIL                                                (* ; "Edited  1-Oct-2024 17:35 by PA")
                                                             (* Define the new card type Web that 
                                                             inherits from Text.)
    (NCP.CreateCardType 'Web 'Text '((MakeFn WCD.MakeWebCard))
           `((LinkAnchorModesSupported NIL)
             (DisplayedInMenuFlg T])

(WCD.MakeWebCard
  [LAMBDA (Card Title NoDisplayFlg)                          (* ; "Edited  3-Oct-2024 13:58 by PA")
                                                             (* ; "Edited  1-Oct-2024 17:51 by PA")

         (* Make a new card of type Web. Return the window of the new card if NoDisplayFlg 
         is non-NIL, and the ID if NoDisplayFlg is NIL.)

    (if (NOT NoDisplayFlg)
        then (LET ((Window (NCP.ApplySuperTypeFn MakeFn Card Card Title NoDisplayFlg)))
                  (NCP.CardProp Card 'URL (WCD.AskURL Window))
                  (NCP.CardTitle Card "Untitled")
                  Window)
      else (NCP.CardProp Card 'URL (WCD.AskURL))             (* TODO More Text card initialization 
                                                             here.)
           Card])

(WCD.TraverseWebLink
  [LAMBDA (Card Window)                                      (* ; "Edited  5-Oct-2024 19:50 by PA")
                                                             (* ; "Edited  4-Oct-2024 18:43 by PA")

         (* Open in the system web browser the URL associated with the destination Web 
         Card. Also opens the card and returns it.
         The Window containing the link icon is ignored.)

    (AND (NCP.ValidCardP Card)
         (LET [(URL (NCP.CardProp Card 'URL]
              (if URL
                  then (NCP.OpenCard Card)
                       (ShellBrowse URL))
              Card])

(WCD.ValidURLP
  [LAMBDA (String)                                           (* ; "Edited  7-Oct-2024 13:21 by PA")

         (* Return String if it is a valid URL, NIL otherwise.
         A URL is valid if it is not null, contains no spaces or tabs, and starts with 
         https%://, http%://, or mailto%:.)

    (LET ((LowerString (L-CASE String)))
         (if [AND LowerString (STRINGP LowerString)
                  (NOT (STRPOS " " LowerString))
                  (LET ((Length (NCHARS LowerString)))
                       (OR (AND (EQP (STRPOS "https://" LowerString)
                                     1)
                                (GREATERP Length 8))
                           (AND (EQP (STRPOS "http://" LowerString)
                                     1)
                                (GREATERP Length 7))
                           (AND (EQP (STRPOS "mailto:" LowerString)
                                     1)
                                (GREATERP Length 7]
             then String
           else NIL])
)

(WCD.CreateWebCardType)

(PUTPROPS Web LinkIconLeftButtonFn WCD.TraverseWebLink)
(DECLARE%: DONTCOPY
  (FILEMAP (NIL (743 4488 (WCD.AskURL 753 . 1411) (WCD.CreateWebCardType 1413 . 1880) (WCD.MakeWebCard 
1882 . 2745) (WCD.TraverseWebLink 2747 . 3410) (WCD.ValidURLP 3412 . 4486)))))
STOP
